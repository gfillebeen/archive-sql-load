--IF OBJECT_ID('[dbo].[Acquisition]', 'U') IS NOT NULL
--	DROP TABLE [dbo].[Acquisition];




--IF OBJECT_ID('[dbo].[Performance]', 'U') IS NOT NULL
--	DROP TABLE [dbo].[Performance];




--POPULATE TABLES



DECLARE @Path NVARCHAR(255) = 'D:\Documents\Programming\Mortgages\Data\'
DECLARE @ColumnTerminator NVARCHAR(5) = CHAR(124)
DECLARE @RowTerminator NVARCHAR(5) = CHAR(10)
DECLARE @Counter INT = 1

WHILE (SELECT COUNT(*) FROM [dbo].[Files_Temporary] WHERE start = 1 AND complete IS null) > 0
BEGIN
    DECLARE @FileName NVARCHAR(255)
    DECLARE @TableName NVARCHAR(255)
	DECLARE @DataYear nvarchar(255)
	DECLARE @CurrentID nvarchar(255)
	DECLARE @msg NVARCHAR(MAX)

    SELECT TOP 1 @CurrentID = ID, @FileName = [FileName], @TableName = [TableName], @datayear = [Year] FROM [dbo].[Files_Temporary] WHERE start = 1 AND complete IS null ORDER BY [FileName] ASC	


	DECLARE @newtable nvarchar(255) =@TableName+'_'+@DataYear



	RAISERROR (@msg, 0, 1) WITH NOWAIT
	IF OBJECT_ID('[dbo].'+@newtable, 'U') IS NULL
	BEGIN

		RAISERROR ('Creating table ...', 0, 1) WITH NOWAIT
		IF @TableName = 'Acquisition'
		BEGIN
			CREATE TABLE [dbo].[tmp_data_table]
			(
				LOAN_IDENTIFIER  VARCHAR(20),
				ORIGINATION_CHANNEL  VARCHAR(1),
				SELLER_NAME VARCHAR(80),
				ORIGINAL_INTEREST_RATE NUMERIC(14,10),
				ORIGINAL_UPB NUMERIC (11,2),
				ORIGINAL_LOAN_TERM NUMERIC (3,0),
				ORIGINATION_DATE VARCHAR(7),
				FIRST_PAYMENT_DATE VARCHAR(7),
				ORIGINAL_LOAN_TO_VALUE  NUMERIC(14,10),
				ORIGINAL_COMBINED_LOAN_TO_VALUE  NUMERIC(14,10),
				NUMBER_OF_BORROWERS  NUMERIC (3,0),
				ORIGINAL_DEBT_TO_INCOME_RATIO  NUMERIC(14,10),
				BORROWER_CREDIT_SCORE_AT_ORIGINATION  NUMERIC (3,0),
				FIRST_TIME_HOME_BUYER_INDICATOR  VARCHAR(1),
				LOAN_PURPOSE VARCHAR(1),
				PROPERTY_TYPE VARCHAR(2),
				NUMBER_OF_UNITS VARCHAR(10),
				OCCUPANCY_TYPE VARCHAR(1),
				PROPERTY_STATE VARCHAR(20),
				ZIP_CODE_SHORT VARCHAR(10),
				PRIMARY_MORTGAGE_INSURANCE_PERCENT NUMERIC(14,10),
				PRODUCT_TYPE  VARCHAR(20),
				CO_BORROWER_CREDIT_SCORE_AT_ORIGINATION NUMERIC(3,0),
				MORTGAGE_INSURANCE_TYPE NUMERIC(1),
				RELOCATION_MORTGAGE_INDICATOR VARCHAR(1)
			);
		END
		ELSE
		BEGIN

			CREATE TABLE [dbo].[tmp_data_table]
			(
				LOAN_IDENTIFIER VARCHAR(20),
				MONTHLY_REPORTING_PERIOD DATE,
				SERVICER_NAME VARCHAR(80),
				CURRENT_INTEREST_RATE NUMERIC(14,10),
				CURRENT_ACTUAL_UPB NUMERIC(11,2),
				LOAN_AGE NUMERIC(10,0),
				REMAINING_MONTHS_TO_LEGAL_MATURITY NUMERIC(3,0),
				ADJUSTED_MONTHS_TO_MATURITY NUMERIC(3,0),
				MATURITY_DATE VARCHAR(7),
				METROPOLITAN_STATISTICAL_AREA VARCHAR(5),
				CURRENT_LOAN_DELINQUENCY_STATUS VARCHAR(5),
				MODIFICATION_FLAG VARCHAR(1),
				ZERO_BALANCE_CODE VARCHAR(2),
				ZERO_BALANCE_EFFECTIVE_DATE VARCHAR(7),
				LAST_PAID_INSTALLMENT_DATE DATE,
				FORECLOSURE_DATE DATE,
				DISPOSITION_DATE DATE,
				FORECLOSURE_COSTS NUMERIC(27,12),
				PROPERTY_PRESERVATION_AND_REPAIR_COSTS NUMERIC(27,12),
				ASSET_RECOVERY_COSTS NUMERIC(27,12),
				MISCELLANEOUS_HOLDING_EXPENSES_AND_CREDITS NUMERIC(27,12),
				ASSOCIATED_TAXES_FOR_HOLDING_PROPERTY NUMERIC(27,12),
				NET_SALE_PROCEEDS NUMERIC(27,12),
				CREDIT_ENHANCEMENT_PROCEEDS NUMERIC(27,12),
				REPURCHASE_MAKE_WHOLE_PROCEEDS NUMERIC(27,12),
				OTHER_FORECLOSURE_PROCEEDS NUMERIC(27,12),
				NON_INTEREST_BEARING_UPB NUMERIC(11,2),
				PRINCIPAL_FORGIVENESS_AMOUNT NUMERIC(11,2),
				REPURCHASE_MAKE_WHOLE_PROCEEDS_FLAG VARCHAR(1),
				FORECLOSURE_PRINCIPAL_WRITE_OFF_AMOUNT NUMERIC(11,2),
				SERVICING_ACTIVITY_INDICATOR VARCHAR(1)
			);
		END	
	create clustered index [id_idx] 
	on dbo.tmp_data_table(LOAN_IDENTIFIER)
	exec sp_rename tmp_data_table, @newtable
	
	end


	 	set @msg = @FileName	+ ': starting ...'
	RAISERROR (@msg, 0, 1) WITH NOWAIT

    DECLARE @SQL NVARCHAR(MAX)
    SET @SQL = '
        BULK INSERT [dbo].[' + @newtable + ']
        FROM ''' + @Path + @FileName + '''
        WITH
        (
            MAXERRORS = 0,
            FIELDTERMINATOR = ''' + @ColumnTerminator + ''',
            ROWTERMINATOR = ''' + @RowTerminator + '''
        )'
    EXEC(@SQL)

	set @msg = @FileName	+  ': done.'
	RAISERROR (@msg, 0, 1) WITH NOWAIT

	UPDATE dbo.Files_Temporary
	SET
	    dbo.Files_Temporary.Complete = 1
	WHERE id = @CurrentID
		

    --SET @Counter = @Counter + 1
END;